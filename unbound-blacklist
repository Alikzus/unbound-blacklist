#!/usr/bin/env perl
#
# unbound-blacklist: Create an include file for unbound with domains to block
#
# Usage: unbound-blacklist [-d] [-f file]
#
# Copyright (c) 2014-2016, Joel A. Nilsson <joel@alikzus.se>
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
# REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND
# FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
# INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
# LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
# OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
# PERFORMANCE OF THIS SOFTWARE.
#

use 5.20.2;

use strict;
use warnings;

use Carp;
use English '-no_match_vars';
use Getopt::Long::Descriptive;
use Readonly;
use POSIX 'strftime';

################################################################################
# PARAMETERS
################################################################################

# The same sources as AdAway for Android, 2016-04-05
Readonly my @SOURCES => (
    'http://hosts-file.net/ad_servers.txt',
    'http://pgl.yoyo.org/adservers/serverlist.php?hostformat=hosts&showintro=0&mimetype=plaintext',
    'http://winhelp2002.mvps.org/hosts.txt',
    'https://adaway.org/hosts.txt',
);

# TODO Replace with getopts
my $BLACKLIST = '/var/unbound/db/blacklist';
#my $BLACKLIST = '/tmp/blacklist.txt';

################################################################################
# CONSTANT DEFINITIONS
################################################################################

Readonly my $PROGRAM_NAME => 'unbound-blacklist';

Readonly my $EMPTY_STRING => '';

Readonly my $FQDN_DOT    => qr/\.$/;
Readonly my $COMMENT     => qr/\s+#.*/;
Readonly my $IP_ADDRESS  => qr/^\d{1,3}\.0\.0\.\d\s+/;
Readonly my $LINE_ENDING => qr/\r?\n$/;

################################################################################
# MAIN
################################################################################

my %domains;
for my $source (@SOURCES) {
    open my $fetch_fh,'-|:encoding(ascii)',"/usr/bin/ftp -Vo - \'$source\'"
        or carp $OS_ERROR;
    while ( my $line = <$fetch_fh> ) {
        my $domain;
        if ( $line =~ s/$IP_ADDRESS/$EMPTY_STRING/xms ) {
            $line =~ s/$LINE_ENDING/$EMPTY_STRING/xms;
            ( $domain = $line ) =~ s/$COMMENT/$EMPTY_STRING/xms;
            $domain =~ s/$FQDN_DOT/$EMPTY_STRING/xms;
            $domain = lc $domain;
        }
        if ( $domain && ( $domain ne 'localhost' ) && ( !$domains{$domain} ) ) {
            $domains{$domain} = 1;
        }
    }
    close $fetch_fh or croak $OS_ERROR;
}

my $timestamp = strftime( "%FT%T", localtime() );
my $content = qq{# Generated with $PROGRAM_NAME, $timestamp\n};

for my $domain ( keys %domains ) {
    $content .= qq{local-zone: "$domain" static\n};
}

system("/bin/mv", "$BLACKLIST", "$BLACKLIST.bak");
open my $blacklist_fh, '>:encoding(ascii)', "$BLACKLIST" or carp $OS_ERROR;
print $blacklist_fh $content;
close $blacklist_fh or croak $OS_ERROR;

my $exit_code;
if ( system("/usr/sbin/unbound-checkconf") ) {
    system( "/bin/mv", "$BLACKLIST", "$BLACKLIST.broken" );
    system( "/bin/mv", "$BLACKLIST.bak", "$BLACKLIST" );
    say STDERR "$PROGRAM_NAME: Invalid unbound configuration";
    $exit_code = 1;
}
else {
    system( "/etc/rc.d/unbound", "reload" );
    say STDOUT "$PROGRAM_NAME: Done";
    $exit_code = 0;
}

exit $exit_code;

################################################################################
# Last updated: 2016-04-08 19:02:35 CEST
